# Revolution Audit Report

### Reviewed by: 0x52 ([@IAm0x52](https://twitter.com/IAm0x52))

### Review Dates: 2/8/24 - 2/25/24

# Scope

The [revolution-protocol](https://github.com/collectivexyz/revolution-protocol/tree/main) repo was reviewed at commit hash [33f69f7](https://github.com/collectivexyz/revolution-protocol/tree/33f69f766b84a85506996cb987d36220e589162d)

In-Scope Contracts
- contracts/packages/protocol-rewards/src/
- contracts/packages/revolution/src/
- contracts/packages/splits/src/

Deployment Chain(s)
- Base Mainnet

# Summary of Findings

|  Identifier  | Title                        | Severity      | Mitigated |
| ------ | ---------------------------- | ------------- | ----- |
| [H-01] | [SplitMain#_distributeERC20 fails to properly distribute and will leave funds permanently stranded](#h-01-splitmain_distributeerc20-fails-to-properly-distribute-and-will-leave-funds-permanently-stranded) | High | <center> ✔️ <center> |
| [H-02] | [SplitMain#withdraw can be used to force user to buy points at bad rates](#h-02-splitmainwithdraw-can-be-used-to-force-user-to-buy-points-at-bad-rates) | High | <center> ✔️ <center> |
| [M-01] | [RevolutionDAOLogicV1 contains unexpected storage variables which can cause critical storage mismatches when upgrading](#m-01-revolutiondaologicv1-contains-unexpected-storage-variables-which-can-cause-critical-storage-mismatches-when-upgrading) | Medium | <center> ✔️ <center> |
| [M-02] | [RevolutionDAOStorageV1#MAX_REFUND_PRIORITY_FEE is too high and can lead to griefing](#m-02-revolutiondaostoragev1max_refund_priority_fee-is-much-too-high) | Medium | <center> ✔️ <center> |
| [M-03] | [RevolutionDAOLogicV1 implements UUPS proxy (ERC1967) but utilizes it with incompatible proxy](#m-03-revolutiondaologicv1-implements-uups-proxy-erc1967-but-utilizes-it-with-incompatible-proxy) | Medium | <center> ✔️ <center> |
| [M-04] | [Token URI will be broken for any image/description/animation_url that has an escaped character and is longer than 255 bytes](#m-04-token-uri-will-be-broken-for-any-imagedescriptionanimation_url-that-has-an-escaped-character-and-is-longer-than-255-bytes) | Medium | <center> ✔️ <center> |
| [L-01] | [AuctionHouse#setCreatorRateBps is missing total percentage validation](#l-01-auctionhousesetcreatorratebps-is-missing-total-percentage-validation) | Low | <center> ✔️ <center> |
| [L-02] | [RevolutionDAOLogicV1#cancel always cancels actions even if they were never queued](#l-02-revolutiondaologicv1cancel-always-cancels-actions-even-if-they-were-never-queued) | Low | <center> ❌ <center> |
| [L-03] | [SplitMain balance view functions return incorrect values for splits](#l-03-splitmain-balance-view-functions-return-incorrect-values-for-splits) | Low | <center> ✔️ <center> |


## [H-01] SplitMain#_distributeERC20 fails to properly distribute and will leave funds permanently stranded

### Details 

The SplitMain contract has been modified to distribute a portion of funds as points rather than ETH. This is represented by the pointsData.percentOfEther variable.

[SplitMain.sol#L232](https://github.com/collectivexyz/revolution-protocol/blob/33f69f766b84a85506996cb987d36220e589162d/packages/splits/src/SplitMain.sol#L232)

    if (_getSum(percentAllocations) + pointsData.percentOfEther != PERCENTAGE_SCALE)

When checking that a split is valid, the above line checks that the sum of account allocations and the percentage used to buy points must sum to 100% (1e6). This allocation is problematic for when distributing ERC20 tokens.

[SplitMain.sol#L815-L821](https://github.com/collectivexyz/revolution-protocol/blob/33f69f766b84a85506996cb987d36220e589162d/packages/splits/src/SplitMain.sol#L815-L821)

    unchecked {
        // cache accounts length to save gas
        uint256 accountsLength = accounts.length;
        for (uint256 i = 0; i < accountsLength; ++i) {
            erc20Balances[token][accounts[i]] += _scaleAmountByPercentage(amountToSplit, percentAllocations[i]);
        }
    }

Points are purchased in ETH and not with ERC20 tokens. We see above that tokens are distributed according to account allocations and never considers pointsData.percentOfEther. Since allocations and percentOfEther must sum to 100% the aboves line will not distribute the full ERC20 balance. This will leave a portion in the contract which is unable to be claimed, resulting in the tokens being lost.

### Lines of Code

[SplitMain.sol#L775-L828](https://github.com/collectivexyz/revolution-protocol/blob/33f69f766b84a85506996cb987d36220e589162d/packages/splits/src/SplitMain.sol#L775-L828)

### Recommendation

The percentage of ETH used to buy points should be applied multiplicatively to the distribution amount similarly to how distributor fees are applied. Additionally the check in validSplit should be adjusted to check that account allocations sum to 1e6 without consideration for pointsData.percentOfEther

### Remediation

Fixed in [PR#134](https://github.com/collectivexyz/revolution-protocol/pull/134). `percentOfEther` is now applied to distributions the same way that distributor fees are. 

## [H-02] SplitMain#withdraw can be used to force user to buy points at bad rates

### Details 

[SplitMain.sol#L533-L556](https://github.com/collectivexyz/revolution-protocol/blob/33f69f766b84a85506996cb987d36220e589162d/packages/splits/src/SplitMain.sol#L533-L556)

    function withdraw(
        address account,
        uint256 withdrawETH,
        uint256 withdrawPoints,
        ERC20[] calldata tokens
    ) external override {
        uint256[] memory tokenAmounts = new uint256[](tokens.length);
        uint256 ethAmount = 0;
        uint256 pointsSold = 0;
        if (withdrawETH != 0) {
            ethAmount = _withdraw(account);
        }
        if (withdrawPoints != 0) {
            pointsSold = _withdrawPoints(account);
        }
        unchecked {
            // overflow should be impossible in for-loop index
            for (uint256 i = 0; i < tokens.length; ++i) {
                // overflow should be impossible in array length math
                tokenAmounts[i] = _withdrawERC20(account, tokens[i]);
            }
            emit Withdrawal(account, ethAmount, tokens, tokenAmounts, pointsSold);
        }
    }

SplitMain#withdraw is a permissionless function that allows anyone to withdraw on behalf of any other user. While not typically problematic, point withdrawals are an exception because they effectively market buy from the emitter. This can be used to grief users and force them to buy at very high token values causes them significant loss of points.

### Lines of Code

[SplitMain.sol#L533-L556](https://github.com/collectivexyz/revolution-protocol/blob/33f69f766b84a85506996cb987d36220e589162d/packages/splits/src/SplitMain.sol#L533-L556)

### Recommendation

When withdrawing points, withdraw should have stricter access control to make sure only the user or user-trusted parties can trigger it.

### Remediation

Fixed in [PR#133](https://github.com/collectivexyz/revolution-protocol/pull/133/). Withdrawing points can now only be done by the account itself, otherwise the transaction will revert.

## [M-01] RevolutionDAOLogicV1 contains unexpected storage variables which can cause critical storage mismatches when upgrading

### Details 

[RevolutionDAOLogicV1.sol#L79-L87](https://github.com/collectivexyz/revolution-protocol/blob/33f69f766b84a85506996cb987d36220e589162d/packages/revolution/src/governance/RevolutionDAOLogicV1.sol#L79-L87)

    /// @notice The name of this revolution
    string public name;

    /// @notice The purpose of this revolution
    string public purpose;

    /// @notice The flag of this revolution
    string public flag;

RevolutionDAOLogicV1 implements an upgradable proxy pattern and offloads all storage variables to RevolutionDAOStorageV1. This allows new storage variables to be appended in later versions. However, by adding storage variables to the logic-only contract, future upgrades to the implementation will cause the variables to be corrupted as the new variables displace them and push them down to later slots.

### Lines of Code

[RevolutionDAOLogicV1.sol#L79-L87](https://github.com/collectivexyz/revolution-protocol/blob/33f69f766b84a85506996cb987d36220e589162d/packages/revolution/src/governance/RevolutionDAOLogicV1.sol#L79-L87)

### Recommendation

Storage variables should be offloaded to RevolutionDAOStorageV1

### Remediation

Fix in commit [bc439f2](https://github.com/collectivexyz/revolution-protocol/commit/bc439f21c343bb62f29de6ddca3532f552e53524). Storage variables have been moved from the logic contract to the storage contract.

## [M-02] RevolutionDAOStorageV1#MAX_REFUND_PRIORITY_FEE is too high and can lead to griefing

### Details 

[RevolutionDAOLogicV1.sol#L121-L122](https://github.com/collectivexyz/revolution-protocol/blob/33f69f766b84a85506996cb987d36220e589162d/packages/revolution/src/governance/RevolutionDAOLogicV1.sol#L121-L122)

    /// @notice The maximum priority fee used to cap gas refunds in `castRefundableVote`
    uint256 public constant MAX_REFUND_PRIORITY_FEE = 2 gwei;

When giving vote refunds, up to 2 gwei of priority fees are refunded to the voter. On Base the typical priority fee is 100 wei. This opens up significant opportunity for users to grief the DOA by using excessive priority fees to drain funds allocated for vote refunds.

### Lines of Code

[RevolutionDAOLogicV1.sol#L121-L122](https://github.com/collectivexyz/revolution-protocol/blob/33f69f766b84a85506996cb987d36220e589162d/packages/revolution/src/governance/RevolutionDAOLogicV1.sol#L121-L122)

### Recommendation

MAX_REFUND_PRIORITY_FEE should be set much lower (i.e. 10,000 wei) to appropriately reflect the average priority fees on Base.

### Remediation

Fixed in [PR#132](https://github.com/collectivexyz/revolution-protocol/pull/132). MAX_REFUND_PRIORITY_FEE has been set to 10,000 wei.

## [M-03] RevolutionDAOLogicV1 implements UUPS proxy (ERC1967) but utilizes it with incompatible proxy

### Details 

[RevolutionDAOProxyV1.sol](https://github.com/collectivexyz/revolution-protocol/blob/33f69f766b84a85506996cb987d36220e589162d/packages/revolution/src/governance/RevolutionDAOProxyV1.sol)

UUPS implementations are designed to function with an ERC1967 proxy, which has a large number of safeguards built in for both upgrades and functionality. By utilizing an incorrect proxy, all of these safeguards are broken. Additionally function name collisions may occur in future upgrades and corrupt or render certain features broken

### Lines of Code

[RevolutionDAOProxyV1.sol](https://github.com/collectivexyz/revolution-protocol/blob/33f69f766b84a85506996cb987d36220e589162d/packages/revolution/src/governance/RevolutionDAOProxyV1.sol)

### Recommendation

Drop RevolutionDAOProxy and utilize standard ERC1967 proxy

### Remediation

Fixed in commit [7c0f1e8](https://github.com/collectivexyz/revolution-protocol/commit/7c0f1e89bebb122e967b152aec3f560667783d5a). DAO proxy has been removed. RevolutionBuilder has been changed to create an ERC1967 proxy instead.

## [M-04] Token URI will be broken for any image/description/animation_url that has an escaped character and is longer than 255 bytes

### Details 

When constructing the TokenURI, special characters must be escaped before they can packed into a JSON to prevent a range of potential issues when displaying/retrieving the image. The contract utilizes the escape function to sanitize strings.

[Descriptor.sol#L105-L134](https://github.com/collectivexyz/revolution-protocol/blob/33f69f766b84a85506996cb987d36220e589162d/packages/revolution/src/Descriptor.sol#L105-L134)

    function escape(string memory str) internal pure returns (string memory) {
        bytes memory strBytes = bytes(str);
        uint8 quotesCount = 0;
        uint256 len = strBytes.length;
        for (uint256 i = 0; i < len; i++) {
            if (strBytes[i] == '"') {
                quotesCount++;
            } else if (strBytes[i] == "\\") {
                quotesCount++;
            } else if (strBytes[i] == "'") {
                quotesCount++;
            }
        }
        if (quotesCount > 0) {
            bytes memory escapedBytes = new bytes(len + (quotesCount));
            uint256 index;
            for (uint8 i = 0; i < len; i++) { <- @audit very prone to overflows
                if (strBytes[i] == '"') {
                    escapedBytes[index++] = "\\";
                } else if (strBytes[i] == "\\") {
                    escapedBytes[index++] = "\\";
                } else if (strBytes[i] == "'") {
                    escapedBytes[index++] = "\\";
                }
                escapedBytes[index++] = strBytes[i];
            }
            return string(escapedBytes);
        }
        return str;
    }

When iterating through the string, it only uses a uint8 for the loop index. This is problematic as any string over the length of 255 will cause the function to revert due to overflow, breaking the URI of the token.

### Lines of Code

[Descriptor.sol#L105-L134](https://github.com/collectivexyz/revolution-protocol/blob/33f69f766b84a85506996cb987d36220e589162d/packages/revolution/src/Descriptor.sol#L105-L134)

### Recommendation

Use a uint256 for i instead of uint8

### Remediation

Fixed in [PR#130](https://github.com/collectivexyz/revolution-protocol/pull/130), as recommended.

## [L-01] AuctionHouse#setCreatorRateBps is missing total percentage validation

### Details 

[AuctionHouse.sol#L240-L247](https://github.com/collectivexyz/revolution-protocol/blob/33f69f766b84a85506996cb987d36220e589162d/packages/revolution/src/AuctionHouse.sol#L240-L247)

    function setCreatorRateBps(uint256 _creatorRateBps) external onlyOwner {
        if (_creatorRateBps < minCreatorRateBps) revert CREATOR_RATE_TOO_LOW();

        if (_creatorRateBps > 10_000) revert INVALID_BPS();
        creatorRateBps = _creatorRateBps;

        emit CreatorRateBpsUpdated(_creatorRateBps);
    }

creatorRate + grantsRate should never exceed 10_000. This is check when initially setting values and is also check when updating the grantsRate. However it is never checked when updating creatorRate. This can lead to scenarios in which the sum of the rates are greater than 10_000 which will DOS the auction house.

### Lines of Code

[AuctionHouse.sol#L240-L247](https://github.com/collectivexyz/revolution-protocol/blob/33f69f766b84a85506996cb987d36220e589162d/packages/revolution/src/AuctionHouse.sol#L240-L247)

### Recommendation

Add the following check:

    if (grantsRateBps + _creatorRateBps > 10_000) revert INVALID_BPS();

### Remediation

Fixed in [PR#131](https://github.com/collectivexyz/revolution-protocol/pull/131), as recommended.


## [L-02] RevolutionDAOLogicV1#cancel always cancels actions even if they were never queued

### Details 

[RevolutionDAOLogicV1.sol#L410-L418](https://github.com/collectivexyz/revolution-protocol/blob/33f69f766b84a85506996cb987d36220e589162d/packages/revolution/src/governance/RevolutionDAOLogicV1.sol#L410-L418)

    for (uint256 i = 0; i < proposal.targets.length; i++) {
        timelock.cancelTransaction(
            proposal.targets[i],
            proposal.values[i],
            proposal.signatures[i],
            proposal.calldatas[i],
            proposal.eta
        );
    }

When canceling a proposal, RevolutionDAOLogicV1#cancel calls DAOExecutor#cancelTransaction for each step of the proposal. When a proposal is queued, the hash of each action is stored as queued. Canceling a proposal will clear this hash. 

This can be used maliciously to prevent valid proposals from executing. If a malicious user queues a transaction at the same time with the same action as another proposal, this can be used to dequeue portions of the legitimate proposal.

Since the protocol operates on Base, the impact of this is low as frontrunning is virtually impossible. However, the fix is straight forwards and will protect in case it is deployed on other chains at a later time.

### Lines of Code

[RevolutionDAOLogicV1.sol#L410-L418](https://github.com/collectivexyz/revolution-protocol/blob/33f69f766b84a85506996cb987d36220e589162d/packages/revolution/src/governance/RevolutionDAOLogicV1.sol#L410-L418)

### Recommendation

Only call timelock.cancelTransaction if the proposal being cancelled is already queued.

### Remediation

Acknowledged by protocol

## [L-03] SplitMain balance view functions return incorrect values for splits

### Details 

When ETH balances are distributed, a portion of the distributed amount is reserved for purchasing points from pointsEmitter. This balance split is not accurately reflected when querying the balances of individual splits.

[SplitMain.sol#L590-L592](https://github.com/collectivexyz/revolution-protocol/blob/33f69f766b84a85506996cb987d36220e589162d/packages/splits/src/SplitMain.sol#L590-L592)

    function getETHBalance(address account) external view returns (uint256) {
        return ethBalances[account] + (splits[account].hash != 0 ? account.balance : 0);
    }

We see that the entire balance of the split is accounted for but when the balances are actually distributed, `only 1 - percentOfEther` will be distributed as actual ETH balance. The result is that this balance is not an accurate reflection of balances. For getETHPointsBalance the inverse is true.

### Lines of Code

[SplitMain.sol#L590-L592](https://github.com/collectivexyz/revolution-protocol/blob/33f69f766b84a85506996cb987d36220e589162d/packages/splits/src/SplitMain.sol#L590-L592)

### Recommendation

Precalculate the distribution of ETH for undistributed balances held by the split.

### Remediation

Fixed in [PR#135](https://github.com/collectivexyz/revolution-protocol/pull/135/files). Balances held by splits are scaled by the points distribution percentage to accurate reflect balances post distribution.
